plugins {
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

if (rootProject.ext.module.is_application) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdk rootProject.ext.android.compileSdkVersion

    defaultConfig {
        if (rootProject.ext.module.is_application) {
            applicationId "com.akashi.user"
            versionCode rootProject.ext.android.versionCode
            versionName rootProject.ext.android.versionName
        }

        minSdk rootProject.ext.android.minSdkVersion
        targetSdk rootProject.ext.android.targetSdkVersion

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            if (rootProject.ext.module.is_application) {
                manifest.srcFile "src/main/AndroidManifest.xml"
            } else {
                manifest.srcFile "src/main/module/AndroidManifest.xml"
            }
        }
    }

    viewBinding {
        enabled = true
    }

    namespace 'com.akashi.user'
}

dependencies {

    // 公共组件
    rootProject.ext.library.each {
        implementation(project(it))
    }
    // module必须的注解处理
    rootProject.ext.module_annotation.each {
        kapt it
    }
    // module通用导入
    rootProject.ext.dependencies.each {
        implementation it
    }

    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.databinding:databinding-runtime:8.0.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}